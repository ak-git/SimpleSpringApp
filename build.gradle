plugins {
    id "org.javamodularity.moduleplugin" version '1.8.11'
    id 'java'
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id "application"
    id "com.github.spotbugs" version '5.0.8'
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

spotbugs {
    reportLevel = 'high'
}

spotbugsMain {
    reports {
        html {
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

jacoco {
    toolVersion = "0.8.8"
}

ext {
    javaVersion = JavaVersion.VERSION_18
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "7.4.2"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2022.07.02'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.classesDirectory
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'jacoco'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        forkEvery = 5
        useJUnitPlatform()
        maxHeapSize = '1G'
    }
}

configurations {
    all {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
}

application {
    mainModule = 'com.ak.spring'
    mainClass = 'com.ak.spring.Application'
}